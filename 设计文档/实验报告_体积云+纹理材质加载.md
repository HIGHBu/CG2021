## 体积云实现

### 云的形状

#### 梯度噪声perlin noise

梯度噪声通过片元的位置和梯度向量得到噪声值，生成具有连续性的纹理

1. 划分网格：对三维空间进行划分，生成三维网格

2. 生成梯度向量。对每个网格的8个顶点生成随机梯度向量

   ```
       //生成随机梯度向量
       vec3 random_perlin(vec3 p) {
           p = vec3(
                   dot(p,vec3(127.1,311.7,69.5)),
                   dot(p,vec3(269.5,183.3,132.7)), 
                   dot(p,vec3(247.3,108.5,96.5)) 
                   );
           return -1.0 + 2.0*fract(sin(p)*43758.5453123);
       }
   ```

3. 根据网格的八个顶点的梯度向量与当前片元点P的距离，赋权进行点积计算

   ```
           float a = dot(random_perlin(i),s);
           float b = dot(random_perlin(i + vec3(1, 0, 0)),s - vec3(1, 0, 0));
           float c = dot(random_perlin(i + vec3(0, 1, 0)),s - vec3(0, 1, 0));
           float d = dot(random_perlin(i + vec3(0, 0, 1)),s - vec3(0, 0, 1));
           float e = dot(random_perlin(i + vec3(1, 1, 0)),s - vec3(1, 1, 0));
           float f = dot(random_perlin(i + vec3(1, 0, 1)),s - vec3(1, 0, 1));
           float g = dot(random_perlin(i + vec3(0, 1, 1)),s - vec3(0, 1, 1));
           float h = dot(random_perlin(i + vec3(1, 1, 1)),s - vec3(1, 1, 1));
   ```

4. 进行平滑插值

   ```
           // Smooth Interpolation
           vec3 u = smoothstep(0.,1.,s);
           // 根据八个顶点进行插值
           return mix(mix(mix( a, b, u.x),
                       mix( c, e, u.x), u.y),
                   mix(mix( d, f, u.x),
                       mix( g, h, u.x), u.y), u.z);
   ```

#### 细胞噪声worly noise

细胞噪声生成晶胞状纹理，用来丰富云的块状结构和蓬松感

根据voronoi图原理，空间被分为多个块状，每个块状有一个中心点称为特征点。空间中的任一点，到其所在区域的特征点的距离相比到其他区域特征点的距离最小。

1. 生成网格，与perlin noise相同

2. 每个网格生成特征点，该特征点在网格内是随机的

   ```
       vec3 random_worly(vec3 p) {
           p = vec3(
                   dot(p,vec3(127.1,311.7,69.5)),
                   dot(p,vec3(269.5,183.3,132.7)), 
                   dot(p,vec3(247.3,108.5,96.5)) 
                   );
           return fract(sin(p)*43758.5453123);
       }
   ```

3. 针对当前片元点p，计算出距离点p最近的特征点v，将点p到点v的距离记为F1

   ```
       float noise_worley(vec3 st) {
           // Tile the space
           vec3 i_st = floor(st);
           vec3 f_st = fract(st);
           float min_dist = 1.;
           for (int i = -1; i <= 1; i++) {
               for (int j = -1; j <= 1; j++) {
                   for (int k = -1; k <= 1; k++) {
                       vec3 neighbor = vec3(float(i),float(j),float(k));
                       vec3 point = random_worly(i_st + neighbor);
                       float d = length(point + neighbor - f_st);
                       min_dist = min(min_dist,d);
                   }
               }
           }
           return pow(min_dist,2.);
       }
   ```

#### 分形布朗运动

分形布朗运动将不同频率和振幅的噪声进行插值计算，使得云的细节更加丰富。

```
    float noise_perlin_fbm(vec3 p) {
        float f = 0.0;
        p = 2. * p;
        float a = 2.;
        for (int i = 0; i < 5; i++) {
            f += a * noise_perlin(p);
            p = 2.0 * p;
            a /= 2.;
        }    
        return f;
    }
    float noise_worley_fbm_abs(vec3 p) {
        float f = 0.0;
        float a = 0.5;
        for (int i = 0; i < 6; i++) {
            f += a * abs(noise_worley(p)-.5);
            p = 2. * p;
            a /= 2.;
        }
        return f;
    }
```

#### 三维噪声纹理生成

根据两种噪声方法，在三维空间中各点生成噪声，噪声值由两噪声混合而成。每个点的噪声值代表该点处的云的密度。

三维噪声示意图：（选自[RayMarching实时体积云渲染入门(上) - 知乎 (zhihu.com)](https://zhuanlan.zhihu.com/p/248406797)）

![img](https://pic3.zhimg.com/80/v2-302cdb5bb7fe380e6b1d6e5cb423f4fa_720w.jpg)

使用噪声时，将其进行插值混合。首先获得两种低频率噪声的分形，作为云的整体效果基础，之后获得两噪声的分形布朗运动模型，对二者进行混合得到最终效果。

```
        float g1 = 0.5+0.5*noise_perlin( q*0.3 );
        float g2 = 0.5+0.5*noise_worley( q*0.3 );
        
        float f1, f2;
        f1 = noise_perlin_fbm(q);
        f2 = noise_worley_fbm_abs(q * oct);
        
        //f1 * a - 0.75, a越大，起伏细节越多
        f1 = mix( f1*0.3-0.75, f1, g1*g1 ) + 0.3;
        //f2 * a - 0.75, a越大，边缘细节越多
        f2 = mix( f2*0.1-0.75, f2, g2*g2 ) + 0.2;
        
        float f = mix(f1, f2, 0.9);
```

到此为止，获得云的基本形状

### 云的绘制

#### 光线步进法

光线步进法从相机位置出发，沿光线方向步进，对每一步进位置的云的密度进行累加，即为需要绘制的云的颜色，将其绘制在片元上。密度由噪声函数计算得到。

![image-20211225224709434](F:\CG\CG2021\设计文档\assets\image-20211225224709434.png)

在绘制时，需要使用透明混合：
$$
color = bgColor * (1 - cvColor.a) + cvColor
$$
$(1 - cvColor.a)$为不透明度，不透明度乘以背景色，最后叠加需要透明的物体的颜色即可进行透明绘制。

光线步进法需要规定云的绘制范围，在本次作业中为了兼顾性能和效果，步进范围为以视点为中心的球壳范围内

![image-20211225225209378](F:\CG\CG2021\设计文档\assets\image-20211225225209378.png)

而半径与视点位置有关，以实现飞机上升时云的远近变化。

#### 体积云光照效果

体积云的光照分为两个部分：

1. 基础颜色

   基础颜色由累计密度决定，云层越厚则越暗。规定云的基础颜色：

   ```
   vec3(1.0,0.95,0.8)  云的亮部基础颜色
   vec3(0.25,0.3,0.35) 云的暗部基础颜色
   ```

   ![image-20211225230204540](F:\CG\CG2021\设计文档\assets\image-20211225230204540.png)

   ![image-20211225230227874](F:\CG\CG2021\设计文档\assets\image-20211225230227874.png)

   基础颜色部分根据云的密度对基础颜色进行线性混合：

   ```
   vec4  col = vec4(mix(vec3(1.0,0.95,0.8), vec3(0.25,0.3,0.35), den), den);
   ```

   此时云具备层次感。

2. 光照

   对于云内一点的亮度确定，需要知道该点与光源之间的云层厚度。根据云层光照颜色的二值性，沿光线方向采样，计算两点之间的密度差，以此粗略估计云层厚度。密度差很大的，说明云层较薄，颜色较亮；密度差小的，说明云层较厚，颜色较暗。

   ![image-20211225231051515](F:\CG\CG2021\设计文档\assets\image-20211225231051515.png)

   根据厚度，加入光照颜色：

   ```
   float dif = clamp((den - map(ro, pos+0.3 * sundir, oct)) / 0.3, 0.0, 1.0 );
   vec3  lin = vec3(0.65, 0.65, 0.75) * 1.1 + 0.8 * vec3(1.0, 0.6, 0.3) * dif;
   ```

最终实现代码：

对于无云的区域，加入no_cloud进行判断。若有一整段距离无云，则判断其邻域均无云。

对于步进距离，较近处需要高精度，而远处不需要很高精度，所以根据距离对步进长度进行变化。

```
    vec4 raymarch( in vec3 ro, in vec3 rd, in vec3 bgcol){
    
        // 确认需要ray march的区域
        float tmin, tmax;

        tmin = abs(ro.y) + 0.1;
        tmax = tmin + 2.;

        float t = tmin;
        
        // raymarch loop
        vec4 sum = vec4(0.0);
        int no_cloud = 0;
        for( int i=0; i<50*kDiv; i++ )
        {
           // step size
           if(no_cloud >= 50)
            break;
           float dt = max(0.3,0.1*t/float(kDiv));
    
           float oct = 1.;

           //cloud
           vec3 pos = ro + t*rd;
           float den = map(ro, pos,oct);
           if( den > 0.01 ){ // if inside
               // do lighting
               float dif = clamp((den - map(ro, pos+0.3 * sundir, oct)) / 0.3, 0.0, 1.0 );
               vec3  lin = vec3(0.65, 0.65, 0.75) * 1.1 + 0.8 * vec3(1.0, 0.6, 0.3) * dif;
               vec4  col = vec4(mix(vec3(1.0,0.95,0.8), vec3(0.25,0.3,0.35), den), den);
               col.xyz *= lin;
               // fog
               col.xyz = mix(col.xyz, bgcol, 1.0 - exp2(-0.003 * t * t));
               // composite front to back
               col.w    = min(col.w * 8.0 * dt,1.0);
               col.rgb *= col.a;
               sum += col*(1.0-sum.a);
           }
           else{
               no_cloud += 1;
           }
           // advance ray
           t += dt;
           // until far clip or full opacity
           if( t>tmax || sum.a>0.99 ) break;
        }
        no_cloud = 0;
        return clamp(sum, 0.0, 1.0);
    }
```

## 模型的纹理与材质加载

### 纹理加载

模型由obj格式文件提供，加载模型即对obj进行解析。

#### obj文件处理

对于o或g, 新建object进行存储；对于v，vt， vn，直接读取点坐标信息并存储；对于f，需要进行进一步处理：

首先将坐标索引、纹理坐标索引、法向量索引vi, ti, ni信息进行解析与存储，之后将非三个点的面划分成一个一个三角形：

```
              if(face.vIndices.length > 3){
                  var n = face.vIndices.length - 2;
                  var newVIndices = new Array(n * 3);
                  var newTIndices = new Array(n * 3);
                  var newNIndices = new Array(n * 3);
                  for(var i=0; i < n; i++){
                    newVIndices[i * 3 + 0] = face.vIndices[0];
                    newVIndices[i * 3 + 1] = face.vIndices[i + 1];
                    newVIndices[i * 3 + 2] = face.vIndices[i + 2];
                    newTIndices[i * 3 + 0] = face.tIndices[0];
                    newTIndices[i * 3 + 1] = face.tIndices[i + 1];
                    newTIndices[i * 3 + 2] = face.tIndices[i + 2];
                    newNIndices[i * 3 + 0] = face.nIndices[0];
                    newNIndices[i * 3 + 1] = face.nIndices[i + 1];
                    newNIndices[i * 3 + 2] = face.nIndices[i + 2];
                  }
                  face.vIndices = newVIndices;
                  face.tIndices = newTIndices;
                  face.nIndices = newNIndices;
              }
```

此处，由于webGL在web端的文件加载是异步进行的，所以在初始化相关Buffer时需要保证纹理图片已加载完毕。在这里，我们将纹理的加载和mtl文件的加载从中分离出来，单独进行，以确保加载正确。

#### mtl文件处理

mtl文件的处理相对简单，只需要将各项材质信息读取出来即可，在外部传出进行处理。

```
    switch(command){
        //comment
        case '#':
            continue;
        //mtl
        case 'newmtl':
          var materialname = sp.getWord();
          var material  = new Material(materialname);
          mtl.mtls.push(material);
          currentmaterial = material;

            // currentmaterial.name = sp.getWord();
            continue;
        //反射指数
        case 'Ns':
            currentmaterial.Ns = sp.getFloat();
            continue;
        //环境反射
        case 'Ka':
            currentmaterial.Ka = sp.getRGB();
            continue;
        //漫反射
        case 'Kd':
            currentmaterial.Kd = sp.getRGB();
            continue;
        //镜面反射
        case 'Ks':
            currentmaterial.Ks = sp.getRGB();
            continue;
        //放射光
        case 'Ke':
            currentmaterial.Ke = sp.getRGB();
            continue;
        //折射值描述
        case 'Ni':
            currentmaterial.Ni = sp.getFloat();
            continue;
        //照明度
        case 'illum':
            currentmaterial.illum = sp.getInt();
            continue;
        // 渐隐指数
        case 'd':
            currentmaterial.d = sp.getFloat();
            continue;
        //为漫反射指定文件
        case 'map_Kd':
            // sp.getWord();sp.getWord();
            // var texname = sp.getWord();
            // mtl.texture = loadTexture(gl, path + texname);
            continue;
        default:
            continue;
```

